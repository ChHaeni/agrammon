*** general ***

author   = Agrammon Group
date     = 2008-04-22
taxonomy = Livestock::FatteningPigs::Housing::KArea

+short

  Calculates correction factor according to the area per animal in
  the house.

+description

  It is assumed that additional areas in stable, as example if the stabel is
  built for more animal, are fully used. 
  So the emissions increase 5\% per 10\% additional area in a stable up to
  the double of the regularized minimal area.

  \begin{todo}{All}{}
	Provide Articles which confirm/justify the assumption. As decided on
	workshop from 2.11.2007.
  \end{todo}


*** input ***

+dimensioning_barn
  type = integer
  adjustBranching = true
  ++labels 
    en = Number of available animal places
    de = Anzahl Tierplätze im Stall
    fr = Nombre de places dans l'étable
  ++units  
    en = -
  ++description
    Number of animals which area allowed in a barn.

*** technical ***

+k_area
  value = 0.5
  ++units  
    en = -
  ++description
    Increasing factor for larger loose housing barns, +10\% excess place leads 
    to +5\% increase in the NH3 emission.

*** external ***

+..::Excretion 

*** output ***

+area_increase
  print = FatteningPigs
  ++units  
    en = -
  ++description
    Factor on what barn size does increase the regularized minimal, limited to 0.5

  ++formula 
    if ( ( Val(animals, ..::Excretion) < In(dimensioning_barn) ) and ( Val(animals, ..::Excretion) != 0 ) ) { 
      if( In(dimensioning_barn) >= (Val(animals, ..::Excretion) * 1.5) ){   
        0.5; 
      }
      else { 
        In(dimensioning_barn) / Val(animals, ..::Excretion) - 1;
      }  			
    }
    else {
      0.0;
    }

+c_area
  print = FatteningPigs
  ++units  
    en = -
  ++description
    correction factor for area per animal

  ++formula
    1 + ( Out(area_increase) * Tech(k_area) );
