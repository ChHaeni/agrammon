*** general ***

author   = Agrammon Group
date     = 2017-07-10
taxonomy = Storage::SolidManure
gui       = Storage::SolidManure,HofdÃ¼ngerlager::Mist,Stockage::Fumier,Storage::Solid manure

+short

 Computes the annual NH3 emission from solid manure storages.
        
+description 

 This process calculates the annual NH3 emission from solid manure
 storage, considering a mean emission rate on TAN flux in solid storage.


*** input ***

*** technical ***

*** external ***

+::Livestock
+SolidManure::Poultry
+SolidManure::Solid

*** output ***



####################################################
#### tan into storage

+tan_into_storage_solid_poultry
  print = 7
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual TAN flux into solid storage from poultry.
  ++formula
    Val(tan_out_livestock_solid, ::Livestock) P*
    Val(share_into_storage_solid_poultry, SolidManure::Poultry);


+tan_into_storage_solid
  print = 1a
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual TAN flux into solid storage from horses etc.. -> Fix: Thomas fragen wegen Bezeichnung!
  ++formula
    Val(tan_out_livestock_solid, ::Livestock) P*
    Val(share_into_storage_solid_no_poultry, SolidManure::Solid);

####################################################

+nh3_nstorage_solid_no_poultry
  print = nh3,storage,solid
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from solid storage.
  ++formula
    Out(tan_into_storage_solid) P*
    Val(er_nh3_nstorage_solid_no_poultry, SolidManure::Solid);

+nh3_nstorage_solid_poultry
  print = 1a
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from poultry manure storage.
  ++formula
    Out(tan_into_storage_solid_poultry) P* 
    Val(er_nh3_nstorage_solid_poultry, SolidManure::Poultry); 


+nh3_nsolid
  print = 1a
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual NH3 emission from storage of solid manure.
  ++formula
    Out(nh3_nstorage_solid_no_poultry) P+
    Out(nh3_nstorage_solid_poultry);


+immobilization_no_poultry
  print = 7
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual TAN immobilized from TAN fraction in solid manure storage.
  ++formula
    (
      Out(tan_into_storage_solid) P-
      Out(nh3_nstorage_solid_no_poultry)
    ) P*
    Val(immobilization_rate_no_poultry, SolidManure::Solid);
 
+immobilization_poultry
  print = 7
  ++units  
    en = kg N/year
    de = kg N/Jahr
    fr = kg N/an
  ++description
    Annual TAN immobilized from TAN fraction in poultry manure storage.
  ++formula
    (
      Out(tan_into_storage_solid_poultry) P-
      Out(nh3_nstorage_solid_poultry)
    ) P*
    Val(immobilization_rate_poultry, SolidManure::Poultry);
