*** general ***

author   = Agrammon Group
date     = 2017-07-10
taxonomy = Livestock::DairyCow::Grazing

+short

 Computes the annual NH3 emission from grazing dairy cows.

+description 

 This process calculates the annual NH3 emission from grazing dairy cows
 based on the total N excreted on the pastures. The N excreted is calculated 
 according to the time the animals spend on the pasture. It assumes that the 
 excretions on the pasture are proportional to the time the animals are grazed.



*** external ***

+Excretion
+Outdoor
+Yard

*** technical ***

+er_dairycow_grazing
  value = 0.083
  ++units  
    en = -
  ++description
    Based on results of Bussink et al. (1992, 1994), an emission factor of 5% N was chosen. Based on a TAN content of 60%, the resulting emission factor is 8.3% TAN (0.050 / 0.600 = 0.083; according to the current content of TAN which is 55% N, an emission factor of 9.1% TAN would result). The previous emission factor of 8.3% TAN is within the range of recent values reported by Bell et al. (2017) and Voglmeier et al. (2018), so the existing of 8.3% of TAN is retained.

\subsubsection{References:}

Bell, M., Flechard, C., Fauvel, Y., Häni, C., Sintermann, J., Jocher, M., Menzi, H., Hensen, A., Neftel, A. 2017. Ammonia emissions from a grazed field estimated by miniDOAS measurements and inverse dispersion modelling. Atmos. Meas. Tech. 10(5): 1875–1892.
Bussink, D.W. 1992. Ammonia volatilization from grassland receiving nitrogen-fertilizer and rotationally grazed by dairy-cattle. Fertil. Res. 33(3): 257-265.
Bussink, D.W., Huijsmans, J.F.M., Ketelaars, J. 1994. Ammonia volatilization from nitric-acid-treated cattle slurry surface applied to grassland. Neth. J. Agric. Sci. 42(4): 293-309.
Voglmeier, K., Jocher, M., Häni, C., Ammann, C. 2018. Ammonia emission measurements of an intensively grazed pasture. Biogeosciences 15(14): 4593-4608.



+er_n2_dairycow_grazing
  value = 0.0
  ++units  
    en = -
  ++description
    Emission factor for manure application. Not considerd relevant

+er_no_dairycow_grazing
  value = 0.0055
  ++units  
    en = -
  ++description
    Emission factor for manure application. Stehfest, Bouwman 2006

+er_n2o_dairycow_grazing
  value = 0.0
  ++units  
    en = -
  ++description
    Emission factor for manure application. ICCP 2006: v4_11Ch_11; Tab11.1


*** input ***

*** output ***

+n_into_grazing
  print = DairyCow
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual total N excretion during grazing for dairy cows.
  ++formula
    Val(n_excretion, Excretion) *
    Val(grazing_hours, Outdoor) / 24 *
    (
      # grazing only (100%)
      (Val(grazing_days, Outdoor) - Val(days_with_grazing_and_yard, Outdoor)) / 365 +
      # shared grazing(+housing) and yard (100% - share yard)
      # this part is split between grazing and housing by the parts grazing_hours & residual
      (1 - Val(share_excretion_with_grazing, Yard)) *
      Val(days_with_grazing_and_yard, Outdoor) / 365
    );

+tan_into_grazing
  print = DairyCow
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual soluble N (TAN) excretion during grazing for dairy cows.
  ++formula
    Val(tan_excretion, Excretion) *
    Val(grazing_hours, Outdoor) / 24 *
    (
      # grazing only (100%)
      (Val(grazing_days, Outdoor) - Val(days_with_grazing_and_yard, Outdoor)) / 365 +
      # shared grazing(+housing) and yard (100% - share yard)
      # this part is split between grazing and housing by the parts grazing_hours & residual
      (1 - Val(share_excretion_with_grazing, Yard)) *
      Val(days_with_grazing_and_yard, Outdoor) / 365
    );

+ef_nh3_ngrazing
  print = DairyCow,ef_nh3
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual total NH3 emission from all grazing dairy cows.
  ++formula
    Tech(er_dairycow_grazing);

+nh3_ngrazing
  print = DairyCow
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual total NH3 emission from all grazing dairy cows.
  ++formula
    Out(tan_into_grazing) * Out(ef_nh3_ngrazing);

+n2_ngrazing
  print = DairyCow
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual total N2 emission from all grazing dairy cows.
  ++formula
    Out(n_into_grazing) * Tech(er_n2_dairycow_grazing);

+no_ngrazing
  print = DairyCow
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual total N2 emission from all grazing dairy cows.
  ++formula
    Out(n_into_grazing) * Tech(er_no_dairycow_grazing);

+n2o_ngrazing
  print = DairyCow
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual total N2O emission from all grazing dairy cows.
  ++formula
    Out(n_into_grazing) * Tech(er_n2o_dairycow_grazing);

+n_remain_grazing
  print = DairyCow
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual N input on pasture.
  ++formula
    Out(n_into_grazing) -
    Out(nh3_ngrazing) - 
    Out(n2_ngrazing) -
    Out(no_ngrazing) - 
    Out(n2o_ngrazing);

+tan_remain_grazing
  print = DairyCow
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual N input on pasture.
  ++formula
    Out(tan_into_grazing) -
    Out(nh3_ngrazing) - 
    Out(n2_ngrazing) -
    Out(no_ngrazing) - 
    Out(n2o_ngrazing);
