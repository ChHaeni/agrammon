*** general ***

author   = Agrammon Group
date     = 2008-02-19
taxonomy = Livestock::DairyCow::Excretion::CFeedSummerRatio

+short

Computes the correction factor for the annual N excretion of a dairy
cow as a function of the summer feed ration.

+description

Computes the correction factor for the annual N excretion of a dairy
cow as a function of the summer feed ration.


 *** input ***

+share_hay_summer
  type = percent
  ++labels 
    en = Proportion of animals receiving hay in summer
    de = Anteil der Tiere, die im Sommer Heu erhalten
    fr = Proportion d'animaux recevant du foin en été
  ++units 
    en = %
  ++description
    Proportion of animals receiving hay in summer.
  ++help
    +++en
      <p>Standard value for Switzerland: 0%</p>
    +++de
      <p>Vorschlag für Standardwert: 0%</p>
    +++fr
      <p>Proposition de valeur standard: 0%</p>
 

+share_maize_silage_summer
  type = percent
  ++labels 
    en = Proportion of animals receiving maize silage in summer
    de = Anteil der Tiere, die im Sommer Maissilage erhalten
    fr = Proportion d'animaux recevant de l'ensilage de maïs en été
  ++units 
    en = %
  ++description
    Proportion of animals receiving maize silage in summer.
  ++help
    +++en
      <p>Standard value for Switzerland: 0%</p>
    +++de
      <p>Vorschlag für Standardwert: 0%</p>
    +++fr
      <p>Proposition de valeur standard: 0%</p>
 

+share_maize_pellets_summer
  type = percent
  ++labels 
    en = Proportion of animals receiving maize pellets in summer
    de = Anteil der Tiere, die im Sommer Maiswürfel erhalten
    fr = Proportion d'animaux recevant des pellets de maïs en été
  ++units 
    en = %
  ++description
    Proportion of animals receiving maize pellets in summer.
  ++help
    +++en
      <p>Standard value for Switzerland: 0%</p>
    +++de
      <p>Vorschlag für Standardwert: 0%</p>
    +++fr
      <p>Proposition de valeur standard: 0%</p>
 

*** technical ***

+c_default_grass
   value = 0.07
   ++units 
     en = -
   ++description
     Modification of annual N excretion by adding hay to the standard 
     ration during the summer feeding period.

+c_hay_summer
  value = -0.03
  ++units 
    en = -
  ++description
    Modification of annual N excretion by adding hay to the standard 
    ration during the summer feeding period.

+c_maize_silage_summer
  value = -0.01
  ++units 
    en = -
  ++description
    Modification of annual N excretion by adding maize silage 
    to the standard ration during summer feeding period.

+c_maize_pellets_summer 
  value = -0.01
  ++units 
    en = -
  ++description
    Modification of annual N excretion by adding maize pellets 
    to the standard ration during summer feeding period.

*** external ***

*** output ***

+share_hay_summer
  print = DairyCow
  ++units 
    en = -
  ++description
    Share
  ++formula
    if (In(share_hay_summer) > 1) {
      In(share_hay_summer) / 100;
    } else {
      In(share_hay_summer);
    }

+share_maize_silage_summer
  print = DairyCow
  ++units 
    en = -
  ++description
    Share
  ++formula
    if ( In(share_maize_silage_summer) > 1 ) {
      In(share_maize_silage_summer) / 100;
    } else {
      In(share_maize_silage_summer);
    }

+share_maize_pellets_summer
  print = DairyCow
  ++units 
    en = -
  ++description
    Share
  ++formula
    if (In(share_maize_pellets_summer) > 1) {
       In(share_maize_pellets_summer) / 100;
    } else {
       In(share_maize_pellets_summer);
    }

+share_grass_only_summer
  print = DairyCow
  ++units 
    en = -
  ++description
    Share
  ++formula
    if ( Out(share_hay_summer) >= Out(share_maize_silage_summer) and 
         Out(share_hay_summer) >= Out(share_maize_pellets_summer) ) {
      1 - Out(share_hay_summer);
    } elsif ( Out(share_maize_silage_summer) >= Out(share_hay_summer) and 
              Out(share_maize_silage_summer) >= Out(share_maize_pellets_summer) ) {
      1 - Out(share_maize_silage_summer);
    } else {
      1 - Out(share_maize_pellets_summer);
    }


+c_summer_ration
  print = DairyCow
  ++units 
    en = -
  ++description
    Summer feed ration correction factor for annual N excretion.
  ++formula
    Tech(c_hay_summer) * Out(share_hay_summer) +
    Tech(c_maize_silage_summer) * Out(share_maize_silage_summer) +
    Tech(c_maize_pellets_summer)  * Out(share_maize_pellets_summer) +
    Tech(c_default_grass) * Out(share_grass_only_summer) ;

