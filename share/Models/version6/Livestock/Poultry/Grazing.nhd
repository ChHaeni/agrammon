*** general ***

author   = Agrammon Group
date     = 2008-02-22
taxonomy = Livestock::Poultry::Grazing

+short

 Computes the NH3 emission, the N excretion and the remaining N of free range poultry per year.

+description 

 This process calculates the annual NH3 emission of free range poultry depending on the free range N excretion and the emission rate. The annual N excretion calculation is based on the grazing hours per day per year and the free range hours per day per year. The annual remaining N from free range poultry is calculated as the annual N excretion minus the annual NH3 emission.

\subsubsection{References:}
Menzi H, Shariatmadari H, Meierhans D, Wiedmer H 1997: Nähr- und Schadstoffbelastung von Geflügelausläufen. Agrarforschung 4: 361-364.

*** external ***

+Excretion
+Outdoor

*** technical ***

+er_free_range
  value = 0.7
  ++units  
    en = -
  ++description
    Emission rate for free range poultry, based on Menzi et al. (1997): 70% of TAN or 28% of Ntot

+er_n2_free_range
  value = 0.0
  ++units  
    en = -
  ++description
    Emission rate for manure application. Not considerd relevant

+er_no_free_range
  value = 0.0055
  ++units  
    en = -
  ++description
    Emission rate for manure application. Stehfest, Bouwman 2006

+er_n2o_free_range
  value = 0.02
  ++units  
    en = -
  ++description
    Emission rate for manure application. ICCP 2006: v4_11Ch_11; Tab11.1


*** input ***


*** output ***

+n_into_grazing
  print = Poultry
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual N excretion free_range (grazing).
  ++formula
    Val(n_excretion, Excretion) *
    Val(free_range_days, Outdoor) / 365 * 
    Val(free_range_hours, Outdoor) / 24 *
    Val(free_range_share, Outdoor)
    ;

+tan_into_grazing
  print = Poultry
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual N excretion free_range (grazing).
  ++formula
    Val(tan_excretion, Excretion) *  
    Val(free_range_days, Outdoor) / 365 * 
    Val(free_range_hours, Outdoor) / 24 *
    Val(free_range_share, Outdoor)
    ;

+ef_nh3_ngrazing
  print = Poultry,ef_nh3
  ++units
     en = -
     de = -
     fr = -
  ++description
    Free_range NH3 emission factor from poultry (grazing). 
  ++formula
    Tech(er_free_range);

+nh3_ngrazing
  print = Poultry
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual free_range NH3 emission from poultry (grazing). 
  ++formula
    Out(tan_into_grazing) * Out(ef_nh3_ngrazing);

+n2_ngrazing
  print = Poultry
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual free_range N2 emission from poultry (grazing). 
  ++formula
    Out(n_into_grazing) * Tech(er_n2_free_range);

+no_ngrazing
  print = Poultry
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual free_range NO emission from poultry (grazing). 
  ++formula
    Out(n_into_grazing) * Tech(er_no_free_range);

+n2o_ngrazing
  print = Poultry
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual free_range N2O emission from poultry (grazing). 
  ++formula
    Out(n_into_grazing) * Tech(er_n2o_free_range);


+n_remain_grazing
  print = Poultry
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual N remaining free_range (on pasture, etc.).
  ++formula
    Out(n_into_grazing) - 
    Out(nh3_ngrazing) - 
    Out(n2_ngrazing) - 
    Out(no_ngrazing) - 
    Out(n2o_ngrazing);

+tan_remain_grazing
  print = Poultry
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual N remaining free_range (on pasture, etc.).
  ++formula
    Out(tan_into_grazing) - 
    Out(nh3_ngrazing) - 
    Out(n2_ngrazing) - 
    Out(no_ngrazing) - 
    Out(n2o_ngrazing);

 
