*** general ***

author   = Agrammon Group
date     = 2017-07-10
taxonomy = Livestock::Equides::Grazing

+short
  Computes the annual NH3 emission from grazing of equides, such as horses, mules, asses. 

+description 

 This process calculates the annual NH3 emission from grazing of equides 
(horses, mules, asses) based on the
 total N excreted on the pastures. The N excreted is calculated according to
 the time the animals spend on the pasture. It assumes that the excretions on
 the pasture are proportional to the time the animals are grazed.

\subsubsection{References:}
Bussink DW 1992. Ammonia volatilization from grassland receiving nitrogen
fertilizer and rotationally grazed by dairy cattle. Fertilizer Research
33:257-265.

Bussink DW 1994. Relationship between ammonia volatilization and nitrogen
fertilizer application rate, intake and excretion of herbage nitrogen by
cattle on grazed swards. Fertilizer Research 38:111-121.


Jarvis SC, Hatch DJ, Lockyer DR 1989. Ammonia fluxes from grazed grassland: annual losses from cattle production systems and their relation to nitrogen inputs. Journal of Agricultural Science 113:99-108.


Peterson SO, Sommer SG, Aaes O, Soegaard K 1998. Ammonia losses from urine and dung of grazing cattle: effect of N intake. Atmospheric environment 32:295-300.


Ross CA, Jarvis SC 2001. Measurement of emission and deposition pattern of ammonia from urine in grass swards. Atmospheric environment 35:867-875.


*** external ***

###+NIntoGrazing (old)

+Excretion
+Outdoor

*** technical ***

+er_equides_grazing
  value = 0.125
  ++units  
    en = -
  ++description
    Emission rate for the calculation of the annual NH3 emission during grazing of equides. 5% Ntot (conversion with a protion of Nsol of 40%: EF 12.5% TAN). The emission rate is derived from Bussink et al. (1992, 1994), Jarvis et al. (1989), Peterson et al. (1998) and Ross and Jarvis (2001).
   (taking into account the generally low fertilization rate of Swiss pastures.)

+er_n2_equides_grazing
  value = 0.0
  ++units  
    en = -
  ++description
    Emission rate for manure application. Not considerd relevant

+er_no_equides_grazing
  value = 0.0055
  ++units  
    en = -
  ++description
    Emission rate for manure application. Stehfest, Bouwman 2006

+er_n2o_equides_grazing
  value = 0.0
  ++units  
    en = -
  ++description
    Emission rate for manure application. ICCP 2006: v4_11Ch_11; Tab11.1

*** input ***

*** output ***

+share_into_grazing
  print = Equides
  ++units
     en = -
     de = -
     fr = -
  ++description
    Share of annual N excretion into grazing.
  ++formula
    # access to grazing only - days with grazing only
    (Val(grazing_days, Outdoor) - Val(days_with_grazing_and_yard, Outdoor)) / 365 * 
    Val(grazing_hours, Outdoor) / 24 +
    # access to grazing only - days with yard and grazing
    Val(days_with_grazing_and_yard, Outdoor) / 365 *
    (Val(grazing_hours, Outdoor) - Val(hours_with_grazing_and_yard, Outdoor)) / 24 +
    # access to yard and grazing (shared 50/50)
    0.5 *
    Val(days_with_grazing_and_yard, Outdoor) / 365 * 
    Val(hours_with_grazing_and_yard, Outdoor) / 24;

+n_into_grazing
  print = Equides
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual N excretion during grazing for equides.
  ++formula
    Val(n_excretion, Excretion) * 
    Out(share_into_grazing);

+tan_into_grazing
  print = Equides
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual solable N (TAN) excretion during grazing for equides.
  ++formula
    Val(tan_excretion,Excretion) * 
    Out(share_into_grazing);

+ef_nh3_ngrazing
  print = Equides,ef_nh3
  ++units
     en = -
     de = -
     fr = -
  ++description
    Annual total NH3 emission from all grazing dairy cows.
  ++formula
    Tech(er_equides_grazing);

+nh3_ngrazing
  print = Equides
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual NH3 emission from equides from grazing.
  ++formula
    Out(tan_into_grazing) * Out(ef_nh3_ngrazing);

+n2_ngrazing
  print = Equides
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual total N2 emission from all grazing dairy cows.
  ++formula
    Out(n_into_grazing) * Tech(er_n2_equides_grazing);

+no_ngrazing
  print = Equides
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual total N2 emission from all grazing dairy cows.
  ++formula
    Out(n_into_grazing) * Tech(er_no_equides_grazing);

+n2o_ngrazing
  print = Equides
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual total N2O emission from all grazing dairy cows.
  ++formula
    Out(n_into_grazing) * Tech(er_n2o_equides_grazing);

+n_remain_grazing
  print = Equides
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual N input on pastures.
  ++formula
    Out(n_into_grazing) -
    Out(nh3_ngrazing) - 
    Out(n2_ngrazing) -
    Out(no_ngrazing) - 
    Out(n2o_ngrazing);

+tan_remain_grazing
  print = Equides
  ++units
     en = kg N/year
     de = kg N/Jahr
     fr = kg N/an
  ++description
    Annual N input on pastures.
  ++formula
    Out(tan_into_grazing) -
    Out(nh3_ngrazing) - 
    Out(n2_ngrazing) -
    Out(no_ngrazing) - 
    Out(n2o_ngrazing);
